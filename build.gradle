//file:noinspection GroovyAssignabilityCheck

plugins() {
  id("java")
  id("org.cadixdev.licenser").version("0.6.1").apply(false)
}

allprojects() {
  apply(plugin: "com.github.spotbugs")
  apply(plugin: "org.cadixdev.licenser")

  setGroup("net.elytrium.limboapi")
  setVersion("1.1.27-SNAPSHOT")

  compileJava() {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  repositories() {
    mavenCentral()

    maven() {
      setName("elytrium-repo")
      setUrl("https://maven.elytrium.net/repo/")
    }
    maven() {
      setName("papermc-repo")
      setUrl("https://repo.papermc.io/repository/maven-public/")
    }
  }
}

String getCurrentShortRevision() {
  OutputStream outputStream = new ByteArrayOutputStream()
  exec {
    if (System.getProperty("os.name").toLowerCase().contains("win")) {
      commandLine("cmd", "/c", "git rev-parse --short HEAD")
    } else {
      commandLine("bash", "-c", "git rev-parse --short HEAD")
    }

    setStandardOutput(outputStream)
  }

  return outputStream.toString().trim()
}
